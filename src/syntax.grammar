@precedence {
  fncallargs @right
  statement @right
  curlyRight @right
  curlyLeft @left
  assign @right
  primary
  or @left
  and @left
  compare @left
  bitwise @left
  bitShift @left
  add @left
  multiply @left
  prefix

  else @right

  containerDecl @left

  // Prevent SR due to eg: a[0..break label:]
  colonSlice
  colonLabel
}

//// Templates.

list<x> { (x ",")* x? }

//// Root.

@top Program { ContainerDocComment? containerMembers }

containerMembers { !containerDecl (ContainerField ",")* (ContainerField | containerDecl*) }

containerDecl { TestDecl | ComptimeDecl | DocComment? Pub? Decl }

//// Top level.

ContainerDocComment { containerComment+ }

TestDecl { Test TestName Block }

TestName { stringLiteralSingle | identifier }

ComptimeDecl { Comptime Block }

Decl {
  (kw<"export"> | (Extern StringLiteralSingle?) | Inline | kw<"noinline">)? FnProto (";" | Block)
  | (kw<"export"> | (Extern StringLiteralSingle?))? kw<"threadlocal">? VarDecl
  | kw<"usingnamespace"> Expr ";"
}

FnProto { fn Identifier? "(" ParamDeclList ")" "!"? TypeExpr }

VarDecl { ( Const | Var ) Name (":" TypeExpr)? ("=" Expr)? ";" }

ContainerField {
  DocComment? Comptime? /*!KEYWORD_fn*/ (Identifier ":")? TypeExpr ByteAlign? ("=" Expr)?
}

Name { identifier }

//// Block level.

Statement {
  (Comptime? VarDecl)
  //| Comptime BlockExprStatement
  | !statement Nosuspend BlockExprStatement
  | Suspend BlockExprStatement
  | Defer BlockExprStatement
  | Errdefer Payload? BlockExprStatement
  | IfStatement
  | !curlyRight labeledStatement
  | !curlyRight SwitchExpr
  | (assignExprExclIf | exprExclIf) ";"
}

IfStatement {
  IfPrefix ( blockExpr (!else Else Payload? Statement)?
             | (AssignExpr | Expr) (";" | !else Else Payload? Statement) )
}

labeledStatement { !curlyRight (Block | LoopStatement) }

LoopStatement { Inline? (ForStatement | WhileStatement) }

ForStatement {
  ForPrefix ( blockExpr (!else Else Statement)?
              | (AssignExpr | Expr) (";" | !else Else Statement) )
}

WhileStatement {
  WhilePrefix ( blockExpr (!else Else Payload? Statement)?
                | (AssignExpr | Expr) (";" | !else Else Payload? Statement) )
}

BlockExprStatement { blockExpr | (AssignExpr | Expr) ";" }

blockExpr { !curlyRight BlockLabel? !curlyRight Block }

//// Expression level.

assignExprExclIf { exprExclIf !assign AssignOp Expr }

AssignExpr { Expr !assign AssignOp Expr }

exprExclIf { binaryExprExclIf | PrefixExpr | !primary primaryExprExclIf }

Expr { BinaryExpr | PrefixExpr | !primary PrimaryExpr }

binaryExprExclIf {
  exprExclIf !or Or Expr
  | exprExclIf !and kw<"and"> Expr
  | exprExclIf !compare CompareOp Expr
  | exprExclIf !bitwise BitwiseOp Expr
  | exprExclIf !bitShift BitShiftOp Expr
  | exprExclIf !add AdditionOp Expr
  | exprExclIf !multiply MultiplyOp Expr
}

BinaryExpr {
  Expr !or Or Expr
  | Expr !and kw<"and"> Expr
  | Expr !compare CompareOp Expr
  | Expr !bitwise BitwiseOp Expr
  | Expr !bitShift BitShiftOp Expr
  | Expr !add AdditionOp Expr
  | Expr !multiply MultiplyOp Expr
}

PrefixExpr { !prefix PrefixOp+ !primary PrimaryExpr }

PrimaryExpr {
  IfExpr
  | BlockLabel? LoopExpr
  | !primary primaryExprExclIf
}

primaryExprExclIf {
  AsmExpr
  | Break BreakLabel? Expr?
  | Comptime Expr
  | !primary Nosuspend Expr
  | Continue BreakLabel?
  | Resume Expr
  | Return Expr?
  | Block
  | curlySuffixExpr
}

curlySuffixExpr { !curlyRight TypeExpr (!curlyRight InitList)? }

InitList {
  "{" FieldInit ("," FieldInit)* ","? "}"
  | "{" Expr ("," Expr)* ","? "}"
  | "{" "}"
}

IfExpr { IfPrefix Expr (!else Else Payload? Expr)? }

Block { "{" Statement* "}" }

LoopExpr {
  // this is for the Inline in SwitchProng
  !curlyRight (Inline (ForExpr | WhileExpr))
  | Inline? (ForExpr | WhileExpr) }

ForExpr { ForPrefix Expr (!else Else Expr)? }

WhileExpr { WhilePrefix Expr (!else Else Payload? Expr)? }

TypeExpr { PrefixTypeOp* (ErrorUnionExpr | suffixExpr) }

ErrorUnionExpr { suffixExpr !curlyRight "!" TypeExpr }

suffixExpr {
  kw<"async"> primaryTypeExpr SuffixOp* FnCallArgs
  | !assign primaryTypeExpr (SuffixOp | FnCallArgs)*
}

primaryTypeExpr {
  BuiltinIdentifier FnCallArgs
  | CharLiteral
  | ContainerDecl
  | "." Identifier
  | "." InitList
  | ErrorSetDecl
  | Float
  | FnProto
  | GroupedExpr
  | LabeledTypeExpr
  | Identifier
  | IfTypeExpr
  | Integer
  //| Comptime TypeExpr
  | Error "." Identifier
  | kw<"anyframe">
  | Unreachable
  | StringLiteral
  | SwitchExpr
}

ContainerDecl { ( Extern | Packed )? ContainerDeclAuto }

ErrorSetDecl { Error "{" list<DocComment? Identifier> "}" }

GroupedExpr { "(" Expr ")" }

IfTypeExpr { IfPrefix TypeExpr (!else Else Payload? TypeExpr)? }

LabeledTypeExpr { BlockLabel Block | BlockLabel? LoopTypeExpr }

LoopTypeExpr {
  // this is for the Inline in SwitchProng
  !curlyRight (Inline (ForTypeExpr | WhileTypeExpr))
  | Inline? (ForTypeExpr | WhileTypeExpr)
}

ForTypeExpr { ForPrefix TypeExpr (!else Else TypeExpr)? }

WhileTypeExpr { WhilePrefix TypeExpr (!else Else Payload? TypeExpr)? }

SwitchExpr { Switch "(" Expr ")" "{" list<SwitchProng> "}" }

//// Assembly.

AsmExpr { kw<"asm"> Volatile? "(" Expr AsmOutput? ")" }

AsmOutput { ":" list<AsmOutputItem> AsmInput? }

AsmOutputItem { "[" Identifier "]" StringLiteral "(" (("->" TypeExpr) | Identifier) ")" }

AsmInput { ":" list<AsmInputItem> AsmClobbers? }

AsmInputItem { "[" Identifier "]" StringLiteral "(" Expr ")" }

AsmClobbers { ":" list<StringLiteral> }

//// Helper grammar.

BreakLabel { !colonLabel ":" Identifier }

BlockLabel { /*left*/ Identifier !colonLabel ":" }

FieldInit { "." Identifier "=" Expr }

WhileContinueExpr { ":" "(" (AssignExpr | Expr) ")" }

AddrSpace { kw<"addrspace"> "(" Expr ")" }

//// Fn specific.

ParamDeclList { list<ParamDecl> }

ParamDecl {
  DocComment? (Noalias | Comptime)? (Identifier ":")? ParamType
  | "..."
}

ParamType { kw<"anytype"> | TypeExpr }

//// Control flow prefixes

IfPrefix { If "(" Expr ")" PtrPayload? }

WhilePrefix { While "(" Expr ")" PtrPayload? WhileContinueExpr? }

ForPrefix { For "(" ForArgList ")" PtrListPayload }

//// Payloads

Payload { "|" Identifier "|" }

PtrPayload { "|" Star? Identifier "|" }

PtrIndexPayload { "|" "*"? Identifier ("," Identifier)* "|" }

PtrListPayload { "|" "*"? Identifier ("," "*"? Identifier)* ","? "|" }

Star { "*" }

//// Switch specific.

SwitchProng { Inline? SwitchCase "=>" PtrIndexPayload? (AssignExpr | Expr) }

SwitchCase { SwitchItem ("," SwitchItem)* ","? | Else }

SwitchItem { Expr ("..." Expr)? }

//// For specific.

ForArgList { ForItem ("," ForItem)* ","? }

ForItem { Expr (".." Expr?)? }

//// Operators

CompareOp { "==" | "!=" | ">" | "<" | ">=" | "<=" }

AssignOp { "*=" | "*|=" | "/=" | "%=" | "+=" | "+|=" | "-=" | "-|=" | "<<=" | "<<|=" | ">>=" | "&=" | "^=" | "|=" | "*%=" | "+%=" | "-%=" | "=" }

BitwiseOp { "&" | "^" | "|" | Orelse | Catch Payload? }

BitShiftOp { "<<" | ">>" | "<<|" }

AdditionOp { "+" | "-" | "++" }

MultiplyOp { "||" | "*" | arithOp<"/"> | "%" | "**" | "*%" | "*|" }

PrefixOp { "!" | "-" | "~" | "-%" | "&" | Try | Await }

PrefixTypeOp {
  "?"
  | kw<"anyframe"> "->"
  | SliceTypeStart (ByteAlign | AddrSpace | Const | Volatile | kw<"allowzero">)*
  | PtrTypeStart (AddrSpace | kw<"align"> "(" Expr (":" Expr ":" Expr)? ")" | Const | Volatile | kw<"allowzero">)*
  | ArrayTypeStart
}

ArrayAccess { "[" Expr "]" }
Slice { "[" Expr ".." (Expr? (!colonSlice ":" Expr)?)? "]" }
Deref { ".*" }
Unwrap { ".?" }
FieldAccess { "." Identifier }

SuffixOp { Slice | ArrayAccess | FieldAccess | Deref | Unwrap }

FnCallArgs { !fncallargs "(" list<Expr> ")" }

//// Ptr specific.

SliceTypeStart { "[" (":" Expr)? "]" }

PtrTypeStart {
  "*"
  | "**"
  // "...extended tokens allow both meanings to take effect, implicitly enabling GLR when both apply."
  // vs kw<"c">, which turns any c into a keyword (when they may instead be identifiers)
  | "[" "*" (@extend<identifier, "c"> | ":" Expr)? "]"
}

ArrayTypeStart { "[" Expr (":" Expr)? "]" }

//// ContainerDecl specific.

ContainerDeclAuto { ContainerDeclType "{" ContainerDocComment? containerMembers "}" }

ContainerDeclType {
  Struct ("(" Expr ")")?
  | Opaque
  | Enum ("(" Expr ")")?
  | Union ("(" (Enum ("(" Expr ")")? | Expr) ")")?
}

//// Alignment

ByteAlign { kw<"align"> "(" Expr ")" }

////

Identifier { identifier }

BuiltinIdentifier { builtinIdentifier }

StringLiteral { stringLiteralSingle | multiStringLiteral }

StringLiteralSingle { stringLiteralSingle }

@skip { whitespace | LineComment }

kw<term> { @specialize[@name={term}]<identifier, term> }
_kw<term> { @specialize<identifier, term> }

Await { _kw<"await"> }
Break { _kw<"break"> }
Catch { _kw<"catch"> }
Comptime { _kw<"comptime"> }
Const { _kw<"const"> }
Continue { _kw<"continue"> }
Defer { _kw<"defer"> }
Else { _kw<"else"> }
Enum { _kw<"enum"> }
Errdefer { _kw<"errdefer"> }
Error { _kw<"error"> }
Extern { _kw<"extern"> }
fn { _kw<"fn"> }
For { _kw<"for"> }
If { _kw<"if"> }
Inline { _kw<"inline"> }
Noalias { _kw<"noalias"> }
Nosuspend { _kw<"nosuspend"> }
Opaque { _kw<"opaque"> }
Or { _kw<"or"> }
Orelse { _kw<"orelse"> }
Packed { _kw<"packed"> }
Pub { _kw<"pub"> }
Resume { _kw<"resume"> }
Return { _kw<"return"> }
Struct { _kw<"struct"> }
Suspend { _kw<"suspend"> }
Switch { _kw<"switch"> }
Test { _kw<"test"> }
Try { _kw<"try"> }
Union { _kw<"union"> }
Unreachable { _kw<"unreachable"> }
Var { _kw<"var"> }
Volatile { _kw<"volatile"> }
While { _kw<"while"> }

@tokens {
  whitespace {
    ($[ \t\r\n] | "\r"? "\n")+
  }

  //identifier: ($) => choice(/[A-Za-z_][A-Za-z0-9_]*/, seq("@", $.string_literal)),
  identifier {
    $[A-Za-z_] $[A-Za-z0-9_]*
  }

  builtinIdentifier { "@" $[A-Za-z_] $[A-Za-z0-9_]* }

  Integer {
    "0b" $[01] ("_"? $[01])*
    | "0o" $[0-7] ("_"? $[0-7])*
    | "0x" $[0-9a-fA-F] ("_"? $[0-9a-fA-F])*
    | $[0-9] ("_"? $[0-9])*
  }

  numeric_<regex> { regex ("_"? regex)* }

  Float {
    "0x" numeric_<$[0-9a-fA-F]> "." numeric_<$[0-9a-fA-F]> ($[pP] $[+\-]? numeric_<$[0-9]>)?
    | numeric_<$[0-9]> "." numeric_<$[0-9]> ($[eE] $[+\-]? numeric_<$[0-9]>)?
    | "0x" numeric_<$[0-9a-fA-F]> $[pP] "-" numeric_<$[0-9]>
    | numeric_<$[0-9]> $[eE] "-" numeric_<$[0-9]>
  }

  @precedence { Float Integer }

  multiStringLiteral { ("\\\\" ![\n]* $[ \n]*)+ }

  stringEscape {
    "\\n" | "\\r" | "\\t" | "\\\\" | "\\'" | "\\\"" | "\\x" $[0-9a-fA-F] $[0-9a-fA-F]
    | "\\u{" $[0-9a-fA-F] ($[0-9a-fA-F] ($[0-9a-fA-F] ($[0-9a-fA-F] ($[0-9a-fA-F] $[0-9a-fA-F]?)?)?)?)? "}"
  }

  CharLiteral {
    "'"
    ( // charFragment
      !['\\]
      | stringEscape
    )*
    "'"
  }

  stringLiteralSingle {
    "\""
    ( // stringFragment
      !["\\]*
      | stringEscape
    )*
    "\""
  }

  LineComment[isolate] { "//" ("\\" (![\n] | "\\r"? "\\n") | ![\n])* }

  DocComment[isolate] { ("///" (![/\n] ![\n]* $[ \n]*|()))+ }

  containerComment { "//!" ![\n]* $[ \n]* }

  arithOp<expr> { expr }

  @precedence { containerComment, DocComment, LineComment, arithOp }
}

@external propSource highlighting from "./highlight"
